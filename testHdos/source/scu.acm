	SPACE	4,10
**	SCU - SETUP CONSOLE USART.
*
*	SCU CONFIGURES THE CONSOLE USART.
*
*	IF 8250
*	  THEN	PORT = 350-7Q
*	  ELSE	PORT = 372-3Q
*
*	ENTRY:	NONE
*	EXIT:	NONE
*	USES:	A,F,B,C,H,L

SCU	LDA	S.CDB
	CPI	CDB.H84
	JZ	SCU1			; IF 8250

*	PRESET 8251

	MVI	A,201Q
	OUT	SC.UART+USR		; GET USART IN KNOWN STATE
	OUT	SC.UART+USR
	OUT	SC.UART+USR
	OUT	SC.UART+USR
	MVI	A,UCI.IR		; RESET
	OUT	SC.UART+USR
	LDA	S.CONTY
	ANI	CTP.2SB
	ERRNZ	CTP.2SB*16+UMI.1B-UMI.2B
	RLC
	RLC
	RLC
	RLC
	ORI	UMI.1B+UMI.L8+UMI.16X
	OUT	SC.UART+USR
	MVI	A,UCI.ER+UCI.RE+UCI.TE
	OUT	SC.UART+USR
	RET

*	IS 8250

SCU1	IN	SC.ACE+UR.LSR
	ANI	UC.TSE			; CHECK FOR SHIFT EMPTY
	JZ	SCU1

	XRA	A
	OUT	SC.ACE+UR.IER		; TURN OFF ANY INTERRUPTS
	MVI	A,UC.LOO+UC.DTR+UC.RTS+UC.OU1				/3.0a/
	OUT	SC.ACE+UR.MCR
	LHLD	S.BAUD
	MVI	A,UC.DLA
	OUT	SC.ACE+UR.LCR		; ACCESS DIVISOR LATCHES
	MOV	A,L
	OUT	SC.ACE+UR.DLL		; SET LEAST SIGNIFICANT
	MOV	A,H
	ANI	177Q			; TRIM STOP BITS
	OUT	SC.ACE+UR.DLM		; SET MOST SIGNIFICANT
	LDA	S.CONTY
	ANI	CTP.2SB
	RRC
	ERRNZ	CTP.2SB/2-UC.2SB
	ERRNZ	UC.2SB-4		; (A) = UC.2SB IF 2 STOP BITS
	ORI	UC.8BW			; 8 BIT WORDS
	OUT	SC.ACE+UR.LCR
	MVI	A,AC.DLY
	CALL	.DLY
	IN	SC.ACE+UR.RBR		; GOBBLE ANY TRASH
	IN	SC.ACE+UR.MCR
	ANI	377Q-UC.LOO
	OUT	SC.ACE+UR.MCR
	RET
