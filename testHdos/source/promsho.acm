PROM$$	LHLD	S.PRMT			; HL => SYSTEM PROMPT BUFFER
	MOV	A,M			; Check for defined SYSTEM PROMPT
	ANA	A
	JZ	PR.P.			; JUST SHOW DEFAULT PROMPT

PROMPT$ EQU	*

PROML	MOV	A,M			; GET CHARACTER
	ANA	A
	RZ				; END OF BUFFER
	PUSH	H			; SAVE POINTER
	CPI	'$'			; IS IT MAGIC CHARACTER
	JNE	PROMX			; NO, TYPE IT
	POP	H			; YES, RESTORE POINTER
	INX	H			; BUMP POINTER
	MOV	A,M			; GET SECOND MAGIC CHARACTER
	ANA	A
	RZ				; PREMATURE END OF BUFFER
	PUSH	H			; SAVE POINTER
	CALL	$MCU			; MAP TO UPPER CASE
	CPI	'D'
	JE	PR.D			; SHOW DATE
	CPI	'T'
	JE	PR.T			; SHOW TIME
	CPI	'V'
	JE	PR.V			; SHOW VERSION #
	CPI	'N'
	JE	PR.N			; SHOW DEFAULT DEVICE
	CPI	'U'
	JE	PR.U			; SHOW DEFAULT UNIT
	CPI	':'
	JE	PR.XXU			; SHOW DEF. DEV. & UNIT & :
	CPI	'X'
	JE	PR.X			; SHOW DEFAULT EXTENSION
	CPI	'E'
	JE	PR.E			; SEND ESCAPE CHARACTER
	CPI	'_'
	JE	PR.NL			; SEND NEW LINE CHARACTER
	CPI	'='
	JE	PR.CR			; SEND CARRIAGE RETURN
	CPI	'^'
	JE	PR.FF			; SEND FORM FEED
	CPI	'H'
	JE	PR.H			; SEND BACKSPACE+SPACE+BACKSPACE
	CPI	'S'
	JE	PR.S			; SEND SPACE
	CPI	','
	JE	PR.C			; SEND TAB
	CPI	'B'
	JE	PR.B			; SEND BELL
	CPI	''''
	JE	PR.TK			; SEND SHORT BELL
	CPI	'P'
	JE	PR.P			; SEND DEFAULT SYSTEM PROMPT
	JMP	PROMX			; WHO KNOWS, JUST SHOW IT

PR.D	LXI	H,S.DATE
	MVI	A,9
	JMP	PROMT

PR.T	MVI	A,200Q
	STA	ENDTIM
	CALL	DISPLA.
	JMP	PROMY

PR.V	CALL	$TYPTX
	DB	VERS/16+'0','.',VERS&0FH+'0','a'+CUSTOM+200Q
	JMP	PROMY

PR.N	LXI	H,S.DFBLK
	MVI	A,2
	JMP	PROMT

PR.U	LDA	S.DFBLK+2
	JMP	PROMX

PR.X	LXI	H,S.DFBLK+3
	MVI	A,3
PROMT	CALL	$TYPCC
	JMP	PROMY

PR.XXU	LXI	H,S.DFBLK
	MVI	A,3
	CALL	$TYPCC
	MVI	A,':'
	DB	1			; FAKE LXI B,
PR.E	MVI	A,ESC
	DB	1
PR.NL	MVI	A,NL
	DB	1
PR.CR	MVI	A,CR
	DB	1
PR.FF	MVI	A,FF
	DB	1
PR.S	MVI	A,' '
	DB	1
PR.C	MVI	A,TAB
	DB	1
PR.B	MVI	A,BELL
	JMP	PROMX

PR.H	CALL	$TYPTX
	DB	BKSP,' ',BKSP+200Q
	JMP	PROMY

PR.P.	DCX	H			; PREDECREMENT FOR *PROMY*
	PUSH	H			; SAVE POINTER

PR.P	LXI	H,DPROM 		; SYSTEM PROMPT
PR.PL	MOV	A,M
	ANA	A
	JZ	PROMY			; AT END OF IT
	SCALL	.SCOUT
	INX	H
	JMP	PR.PL

PR.TK	LDA	ULTRA
	ANA	A
	JZ	PROMY
	MVI	A,CTLR			; Tick sound

PROMX	SCALL	.SCOUT			; SHOW CHARACTER
PROMY	POP	H			; RESTORE POINTER
	INX	H			; BUMP POINTER
	JMP	PROML			; MORE TO GO
