	SPACE	4,10
**	FGC - FREE GROUP CHAIN.
*
*	FGC UNCHAINS A LIST OF GROUPS AND ENTERS THEM INTO THE
*	FREE CHAIN. THIS CAUSES THE SPACE TO BE RETURNED TO THE FREE
*	POOL. NOTE THAT THE FREE CHAIN IS KEPT IN ORDER.
*
*	ENTRY	(A) = 1ST GROUP NUMBER
*		(HL) = GRT ADDRESS
*	EXIT	NONE
*	USES	A,F,D,E,H,L

FGC	INX	H
	MVI	M,1			; FLAG CHANGE
FGC0	ANA	A
	RZ				; NO MORE IN CHAIN
	MOV	L,A
	MOV	A,M			; (A) = 2ND GROUP IN CHAIN TO FREE
	PUSH	PSW			; SAVE FOR NEXT PASS
	MOV	D,L			; (D) = # OF GROUP TO FREE

*	SCAN FREE CHAIN FOR THE RIGHT SPOT

	XRA	A			; (A) = 0
FGC1	MOV	L,A			; (L) = INDEX OF NEXT GROUP
	MOV	A,M			; FOLLOW FREE CHAIN
	ANA	A
	JZ	FGC2			; AT END, MUST PUT NEW ONE HERE
	CMP	D
	CE	S.FASER 		; GROUP IS ALREADY FREE!
	JC	FGC1			; NOT FAR ENOUGH YET

*	FOUND THE PROPER SPOT. 
*
*	(L) = PRECEDING GROUP #
*	(D) = INDEX OF GROUP TO FREE

FGC2	MOV	M,D			; POINT TO FREED BYTE
	MOV	L,D
	MOV	M,A			; FREE POINTS TO NEXT
	POP	PSW
	JMP	FGC0			; DO NEXT
