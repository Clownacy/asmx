$INCHA	SPACE 4,10
**	$INCHA - READ ONE CHARACTER.
*
*	$INCHA READS ONE CHARACTER FROM THE TERMINAL.
*
*	CHAR = CTL-U: ERASE LINE
*	     = BKSP: BACKSPACE CHARACTER
*		= RUBOUT: BACKSPACE CHARACTER

**************************************************************************
**									**
	ERRNZ	1		THIS ROUTINE IS OBSOLETE		**
**									**
**************************************************************************


$INCHA	CALL	$RCHAR		READ A CHARACTER
	CPI	BKSP
	JE	INC0		IS BKSP
	CPI	RUBOUT
	JE	INC0		IS RUBOUT
	PUSH	PSW		SAVE CODE
	LDA	$INCHAA		(A) = RUBOUT FLAG
	ANA	A
	CNZ	$WCHAR		ECHO RUBOUT CHAR, IF ANY
	XRA	A
	STA	$INCHAA		CLEAR FLAG
	POP	PSW
	CPI	'U'-'@'
	RNE			NOT CTL-U, RETURN

*	IS CTL-U

	LXI	H,LINE
	CALL	$CRLF
	JMP	INC1		CLEAR LINE AND SET LINPTR

*	IS BKSP

INC0	LHLD	LINPTR
	MVI	A,#LINE
	CMP	L
	JE	$INCHA		IF ALREADY AT FRONT
	DCX	H
	LDA	S.CONTY		SEE IF BACKSPACING
	ANA	A
	JP	INC3		IS NON-CRT
	CALL	$TYPTX
	DB	BKSP,' ',BKSP+200Q	BACKSPACE FOR CRT
INC1	SHLD	LINPTR
	MVI	M,0		CLEAR ENTRY
	JMP	$INCHA		AGAIN

*	BACKSPACE FOR NON-CRT

INC3	LDA	$INCHAA		(A) = FLAG
	ANA	A
	JNZ	INC4		AM STILL BACKSPACING
	MVI	A,'/'
	STA	$INCHAA		SET FLAG
	CALL	$WCHAR		TYPE
INC4	MOV	A,M
	CALL	$WCHAR		SHOW CHARACTER BEING REMOVED
	JMP	INC1		CLEAR IT

$INCHAA	DB	0		RUBOUT FLAG
