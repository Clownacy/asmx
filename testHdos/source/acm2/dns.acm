	SPACE	4,10
**	$DNS - DECODE NUMERIC SWITCH.
*
*	$DNS DECODES A NUMERIC SWITCH OF THE FORM:
*
*	:NNN
*
*	A POSTRADIX OF D, Q, O, OR B IS ALLOWED. IF THE VALUE
*	IS SYNTATICALLY VALID, IT IS REPLACED WITH BLANKS.
*
*	ENTRY	(HL) = ADDRESS IF ':'
*		(A) = DEFAULT BASE (2, 8 OR 10)
*	EXIT	'C' CLEAR IF OK
*		 (HL) ADVANCED PAST VALUE
*		 VALUE BLANKED
*		 (DE) = VALUE
*		'C' SET IF ERROR
*	USES	ALL

$DNS.	MVI	A,10			; BASE 10 DEFAULT

$DNS	MOV	B,A			; (B) = DEFAULT BASE
	MOV	A,M
	CPI	':'
	STC
	RNE				; NOT ':'
	PUSH	H			; SAVE ADDRESS OF SWITCH START
	INX	H
	MOV	A,B
	CALL	$DNV			; DECODE NUMERIC VALUE
	POP	B			; (BC) = ADDRESS OF ':'
	RC				; ERROR

$DNS1	MVI	A,' '
	STAX	B			; BLANK LINE
	INX	B			; INCREMENT ADDRESS
	MOV	A,L
	CMP	C
	JNE	$DNS1
	MOV	A,B
	CMP	H			; SEE IF IN RIGHT BANK
	JNE	$DNS1

	RET				; RETURN WITH 'C' CLEAR AND VALUE
