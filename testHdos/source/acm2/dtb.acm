$DTB	SPACE	4,10
**	$DTB - DELETE TRAILING BLANKS.
*
*	$DTB DELETES THE TRAILING BLANKS FROM A CODED LINE.
*
*	ENTRY	(HL) = LINE FWA
*	EXIT	(A) = LENGTH OF RESULT (ENCLUDING 00 TERMINATOR BYTE)
*	USES	A,F


$DTB	PUSH	D		SAVE (DE)
	MOV	D,H
	MOV	E,L		(DE) = FWA
	DCX	D		(DE) = FWA-1
$DTB1	MOV	A,M
	INX	H
	ANA	A		FIND END OF LINE
	JNZ	$DTB1
	DCX	H		(HL) = ADDRESS OF TERMINATING ZERO BYTE

*	GOT END OF LINE. DELETE TRAILING BLANKS

$DTB2	DCX	H		BACKUP ONE CHARACTER
	CALL	$CDEHL
	JE	$DTB3		GONE PAST FRONT OF LINE, MUST BE ALL BLANKS
	MOV	A,M
	CPI	' '
	JE	$DTB2		GOT BLANK

*	HAVE TRIMED LINE. COMPUTE LENGTH

$DTB3	INX	H
	MVI	M,0		TERMINATE LINE
	MOV	A,L
	SUB	E		(A) = LENGTH +1 (FOR 00 BYTE)
	XCHG
	INX	H		(HL) = LINE FWA
	POP	D		RESTORE (DE)
	RET
