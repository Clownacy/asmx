FREAL	SPACE	4,10
**	$FREAL - READ BYTES FROM FILE BUFFER.
*
*	$FREAL IS CALLED TO READ A NUMBER OF BYTES FROM A FILE BUFFER.
*
*	ENTRY	(BC) = BYTE COUNT
*		(DE) = FWA FOR BYTES
*		(HL) = ADDRESS OF FILE BUFFER
*	EXIT	TO *FERROR* IF ERROR
*		TO CALLER IF OK
*		 (BC) = UNREAD BYTE COUNT (ONLY IF EOF)
*		 (DE) = ADDRESS OF FIRST UNUSED BYTE
*		 'C' SET IF EOF DURING READ
*	USES	A,F,B,C,D,E

$FREAL	CALL	$FREAL.
	RNC				; RETURN IF OK
	CPI	EC.EOF
	JNE	$FERROR 		; ERROR IS NOT EOF
	STC
	RET				; ERROR IS SIMPLY EOF


$FREAL. DCX	B			; (BC) = COUNT NOT ENCLUDING 00 BYTE
	XRA	A
	STA	EOFFLG			; CLEAR EOF FLAG
	PUSH	H
	CALL	CBT			; COPY BUFFER POINTERS TO TEMP CELLS

*	COPY DATA FROM BUFFER TO TARGET

$REAL2	PUSH	D			; SAVE TARGET ADDRESS
	LDA	T.FLG
	ANI	FT.OR
	MVI	A,EC.FNO
	STC				; ASSUME FILE NOT OPEN
	JZ	$REAL8			; ERROR
	MOV	A,B
	ORA	C
	JZ	$REAL8			; ALL DONE

*	COMPUTE MIN( DATA IN BUFFER, DATA REQUESTED)

$REAL3	LHLD	T.PTR
	XCHG				; (DE) = (FB.PTR) = ADDRESS OF DATA
	LHLD	T.LIM			; (HL) = LIMIT ADDRESS
	MOV	A,L
	SUB	E
	MOV	L,A
	MOV	A,H
	SBB	D
	MOV	H,A			; (HL) = NUMBER OF BYTES IN BUFFER
	MOV	A,C
	SUB	L			; COMPARE TO REQUESTED COUNT
	MOV	A,B
	SBB	H
	JNC	$REAL4			; LESS THAN REQUESTED COUNT
	MOV	H,B
	MOV	L,C			; DONT TRANSFER MORE THAN LIMIT
$REAL4	MOV	A,H
	ORA	L
	JNZ	$REAL6			; SOME IN BUFFER

*	BUFFER IS EMPTY. RE-FILL IT

	CALL	$FFB			; FILL FILE BUFFER
	JC	$REAL8			; ERROR CONDITION
	JMP	$REAL3			; COUNT THE DATA

*	GOT THE DATA. MOVE IT FROM BUFFER TO TARGET
*
*	(BC) = LIMIT COUNT
*	(DE) = FROM
*	(HL) = COUNT
*	((SP)) = TO

$REAL6	MOV	A,C
	SUB	L
	MOV	C,A
	MOV	A,B
	SBB	H
	MOV	B,A			; REMOVE BYTES FROM REQUEST COUNT
	PUSH	B
	XTHL				; (HL) = REMAINING REQUEST COUNT
	POP	B			; (BC) = COUNT FOR THIS COPY
	XTHL				; (HL) = TARGET ADDR, ((SP)) = REMAINING
$REAL7	LDAX	D
	INX	D
	MOV	M,A
	INX	H
	ANA	A			; SEE IF 00 BYTE
	JNZ	$REL7.3 		; NOT 00

*	IS 00 BYTE. IGNORE IT

	XTHL
	INX	H			; ADD ONE TO UNREQUITED COUNT
	XTHL
	DCX	H			; BACKSPACE OVER CHARACTER
$REL7.3	DCX	B
	CPI	NL
	JE	$REL7.5 		; IS END OF LINE
	MOV	A,B
	ORA	C
	JNZ	$REAL7			; MORE TO GO
	XCHG
	SHLD	T.PTR			; UPDATE POINTER
	POP	B			; (BC) = REMAINING COUNT
	JMP	$REAL2			; SEE IF MORE IN BUFFER

*	END OF CODED LINE

$REL7.5	XCHG
	DCX	D			; BACK OVER NL CHARACTER
	SHLD	T.PTR			; UPDATE POINTER
	POP	B			; (BC) = REMAINING COUNT
	PUSH	D			; SAVE TARGET LWA

*	READ COMPLETE.
*
*	(PSW) = COMPLETION FLAGS

$REAL8	POP	D			; RESTORE TARGET ADDRESS
	PUSH	PSW			; SAVE RETURN CODE
	XRA	A
	STAX	D			; FLAG END OF LINE
	POP	PSW			; RESTORE RESULT FLAGS
	INX	D			; POINT TO NEXT FREE
$REAL9	POP	H
	JMP	CTB			; COPY TEMP POINTERS BACK TO BLOCK, EXIT
