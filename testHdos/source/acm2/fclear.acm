FCLEAR	SPACE	4,10
**	$FCLEAR - CLEAR FILE BLOCK.
*
*	$FCLEAR CLEARS OUT A FILE BLOCK BY SETTING THE POINTERS TO
*	EMPTY, AND CLEARING ANY ERROR OR EOF FLAGS.
*
*	THE DISK (OR WHATEVER) FILE IS NOT POSITIONED, READ, WRITEN
*	OPENED OR CLOSED.
*
*	ENTRY	(HL) = FB ADDRESS
*	EXIT	NONE
*	USES	A,F,B,C

$FCLEAR	EQU	*

	PUSH	H			; SAVE FILE BLOCK ADDRESS
	ERRNZ	FB.FLG-FB.CHA-1
	INX	H
	ERRNZ	FB.FWA-FB.FLG-1
	INX	H			; (HL) = #FB.FWA
	MOV	C,M
	INX	H
	MOV	B,M			; (BC) = FB.FWA
	INX	H
	ERRNZ	FB.PTR-FB.FWA-2
	MOV	M,C			; SET FB.PTR = FB.FWA
	INX	H
	MOV	M,B
	INX	H
	ERRNZ	FB.LIM-FB.PTR-2
	MOV	M,C			; SET FB.LIM = FB.FWA
	INX	H
	MOV	M,B
	POP	H			; (HL) = FB FWA
	RET
