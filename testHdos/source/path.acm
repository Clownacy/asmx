***	PATH - SET/SHOW SYSTEM PATH
*
*	PA[TH]			- SHOW CURRENT PATH STRING
*	PA[TH] path string	- SET NEW PATH STRING
*	PA[TH] ~		- CLEAR PATH STRING

PATH	POP	H
	CALL	M$SOB			; Map + Skip over blanks
	ANA	A
	JZ	PATSHOW 		; Show us what you got
	CPI	'~'
	JE	PATCLR			; Clear it

*	SET NEW PATH STRING UNTIL LIMIT OR NULL

	XCHG
	LHLD	S.PATH			; BUFFER
	XCHG
	MVI	C,100			; CHAR. LIMIT
PAT1	MOV	A,M
	STAX	D
	ANA	A
	JZ	SYSCMD. 		; FOUND NULL
	INX	H
	INX	D
	DCR	C
	JNZ	PAT1
	XRA	A
	STAX	D			; FORCE FINAL NULL
	RST	SYSCMD			; REACHED LIMIT

PATSHOW CALL	$TYPTX
	DB	NL,'PATH ','"'+200Q
	LHLD	S.PATH			; PRINT STRING DATA
PAT2	MOV	A,M
	ANA	A
	JZ	PAT3			; UNTIL WE FIND A NULL BYTE
	SCALL	.SCOUT
	INX	H
	JMP	PAT2

PAT3	MVI	A,'"'
	SCALL	.SCOUT
	RST	SYSCMD

PATCLR	XRA	A
	LHLD	S.PATH
	MOV	M,A			; Clear PATH string
	RST	SYSCMD
