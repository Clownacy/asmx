	SPACE	4,10
***	$FST - FIND IN SERIAL TABLE.
*
*	$FST SEARCHES A SERIAL TABLE FOR A SPECIFIC KEY.
*
*	ENTRY:	(HL) = ADDRESS OF TABLE
*		(DE) = ADDRESS OF SEARCH KEY
*	EXIT:	(DE) UNCHANGED
*		'Z' CLEAR IF NO MATCH FOUND
*		 (HL) = ADDRESS OF NEXT AVAILABLE BYTE
*		'Z' SET IF MATCH FOUND
*		 (HL) = ADDRESS OF FIRST DATA BYTE
*	USES:	A,F,H,L

$FST	PUSH	B			; SAVE REGISTERS
	PUSH	D

FST1	POP	D			; RESTORE ADDRESS OF SEARCH KEY
	PUSH	D

FST1A	MOV	A,M			; GET A CHARACTER
	ANI	177Q			; REMOVE PARITY
	JZ	FST4			; HAVE COMPLETE MATCH
	MOV	B,A			; SAVE THE CHARACTER
	LDAX	D			; CHARACTER WE WANT TO MATCH
	ANI	177Q			; Remove parity
	CMP	B			; COMPARE IT WITH TABLE
	JNE	FST2			; NOT THE SAME
	INX	D			; BUMP POINTER
	INX	H
	JMP	FST1A			; GO DO NEXT CHARACTER

*	HAVE MIS-MATCH.  SEE IF MISSING CHARACTER IS SIGNIFICANT.

FST2	MOV	A,M
	INR	A			; check for optional number
	JZ	FST2A			; check for it
	DCR	A
	JM	FST4			; YEP, HAVE A MATCH.
FST2A	MOV	A,M			; GET CHAR. BACK FROM KEY
	ANI	177Q			; mask off high bit
	CPI	177Q			; IS NUMBER ACCEPTABLE?
	JNZ	FST3			; NO.

*	Check if optional

	LDAX	D			; GET THE CHARACTER
	ANI	177Q			; Trim parity
	CPI	'0'			; CHECK FOR 0-7
	JC	FST2B			; check if optional specified
	CPI	'7'+1
	INX	D			; BUMP PAST IT
	JC	FST4			; ONE FOR THE GOOD GUYS!
	JMP	FST3			; skip this key

FST2B	MOV	A,M			; get the table key back
	ORA	A			; is high bit set ?
	JM	FST4			; han'l it, pronto

*	SKIP OVER THIS KEY

FST3	MOV	A,M			; GET CHARACTER
	INX	H			; BUMP DOWN
	CPI	200Q			; END OF KEY?
	JNE	FST3			; NO, KEEP GOING
	INX	H			; START OF NEXT SWITCH
	MOV	A,M			; 1ST CHARACTER
	ANA	A			; IF IT'S NULL, END OF TABLE
	JNZ	FST1			; OK TO KEEP TRYING
	INR	A			; CLEAR 'Z' SINCE NO MATCH
	POP	D			; PUT REGISTERS BACK
	POP	B
	RET				; WITH NO MATCH

*	HAVE A MATCH.  FIND DATA CHARACTER AFTER KEY.

FST4	LDAX	D			; GET LAST CHARACTER OF KEY
	ANA	A			; BETTER BE NULL
	JNZ	FST3			; NO MATCH HERE.
FST4A	MOV	A,M			; GET CHARACTER FROM TABLE
	CPI	200Q			; END?
	INX	H
	JNE	FST4A			; KEEP LOOKING
	XRA	A			; SET 'Z' SINCE HAVE MATCH
	POP	D			; RESTORE
	POP	B
	RET				; WITH MATCH
