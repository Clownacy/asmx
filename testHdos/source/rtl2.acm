	SPACE	4,10
**	$RTL - READ TEXT LINE.
*
*	$RTL READS A LINE FROM THE TERMINAL.
*
*	CHARACTER ARE ACCEPTED FROM THE TERMINAL, RUBOUT AND BACKSPACE
*	CHARACTERS ARE PROCESSED. WHEN A CARRIAGE RETURN IS ENTERED,
*	$RTL RETURNS.
*
*	IF USER SETS:
*
*	$MULTI	EQU	0
*
*	THEN USER CAN ENTER MULTIPLE COMMANDS ON ONE
*	LINE SEPERATED BY A '\'
*
*	ENTRY	(HL) = BUFFER FWA
*	EXIT	'C' CLEAR IF OK
*		 DATA IN BUFFER
*		 (A) = TEXT LENGTH
*		'C' SET IF CTL-D STRUCK
*	USES	A,F

$RTL.	CALL	$RTL		$RTL IN UPPER CASE
	RC			CTL-D
	JMP	$MLU		MAP LINE TO UPPER CASE

$RTL	EQU	*
	PUSH	H		SAVE FWA

$RTL1	EQU	*
	IF	$BATCH
	LDA	S.FLAG
	ANI	S.BATCH
	JZ	$RTLX
	CALL	B.BYTE
	JMP	$RTLXX
	ENDIF

$RTLX	SCALL	.SCIN		READ CHARACTER
	JC	$RTLX

$RTLXX	CPI	CTLD
	JE	$RTL4		CTL-D STRUCK
	MOV	M,A
	INX	H

	IF	$BATCH
	ANA	A
	JZ	$RTL3
	ENDIF

	IF	$MULTI
	CPI	'\'
	JNE	$RTLY
	LDA	S.FLAG
	ORI	S.TABUF
	STA	S.FLAG
	JMP	$RTL2

$RTLY	EQU	*
	ENDIF

	CPI	NL
	JNE	$RTL1
$RTL2	DCX	H
	MVI	M,0
	INX	H

*	ALL DONE. COMPUTE LENGTH

$RTL3	XCHG			(DE) = LWA+1
	XTHL			(HL) = FWA
	MOV	A,E
	SUB	L		(A) = LENGTH
	ANA	A		CLEAR CARRY
	POP	D		RESTORE (DE)
	RET

*	CTL-D STRUCK

$RTL4	CALL	$TYPTX
	DB	'^D',ENL
	POP	H		(HL) = FWA
	STC
	RET
