FREAB	SPACE	4,10
**	$FREAB - READ BYTES FROM FILE BUFFER.
*
*	$FREAB IS CALLED TO READ A NUMBER OF BYTES FROM A FILE BUFFER.
*
*	ENTRY	(BC) = BYTE COUNT
*		(DE) = FWA FOR BYTES
*		(HL) = ADDRESS OF FILE BUFFER
*	EXIT	TO *FERROR* IF ERROR
*		TO CALLER IF OK
*		 (BC) = UNREAD BYTE COUNT (ONLY IF EOF)
*		 (DE) = ADDRESS OF FIRST UNUSED BYTE
*		 'C' SET IF EOF DURING READ
*	USES	A,F,B,C,D,E

$FREAB	CALL	$FREAB.
	RNC				; RETURN IF OK
	CPI	EC.EOF
	JNE	$FERROR 		; ERROR IS NOT EOF
	STC
	RET				; ERROR IS SIMPLY EOF


$FREAB. XRA	A
	STA	EOFFLG			; CLEAR EOF FLAG
	PUSH	H
	CALL	CBT			; COPY BUFFER POINTERS TO TEMP CELLS

*	COPY DATA FROM BUFFER TO TARGET

$REAB2	PUSH	D			; SAVE TARGET ADDRESS
	LDA	T.FLG
	ANI	FT.OR
	MVI	A,EC.FNO		; ASSUME FILE NOT OPEN FOR READ
	STC
	JZ	$REAB8			; NOT OPEN FOR READ
	MOV	A,B
	ORA	C
	JZ	$REAB8			; ALL DONE

*	COMPUTE MIN( DATA IN BUFFER, DATA REQUESTED)

$REAB3	LHLD	T.PTR
	XCHG				; (DE) = (FB.PTR) = ADDRESS OF DATA
	LHLD	T.LIM			; (HL) = LIMIT ADDRESS
	MOV	A,L
	SUB	E
	MOV	L,A
	MOV	A,H
	SBB	D
	MOV	H,A			; (HL) = NUMBER OF BYTES IN BUFFER
	MOV	A,C
	SUB	L			; COMPARE REQUESTED TO AVAILABLE
	MOV	A,B
	SBB	H
	JNC	$REAB4			; MORE REQUESTED THEN AVAILABLE
	MOV	H,B
	MOV	L,C			; LIMIT TRANSFER TO REQUEST COUNT
$REAB4	MOV	A,H
	ORA	L
	JNZ	$REAB6			; SOME IN BUFFER

*	BUFFER IS EMPTY. RE-FILL IT

	CALL	$FFB			; FILL FILE BUFFER
	JC	$REAB8			; ERROR CONDITION
	JMP	$REAB3			; COUNT NEW DATA

*	GOT THE DATA. MOVE IT FROM BUFFER TO TARGET
*
*	(BC) = REQUESTED COUNT
*	(DE) = FROM
*	(HL) = COUNT
*	((SP)) = TO

$REAB6	MOV	A,C
	SUB	L
	MOV	C,A
	MOV	A,B
	SBB	H
	MOV	B,A			; REMOVE BYTES FROM REQUEST COUNT
	PUSH	B
	XTHL				; (HL) = REMAINING REQUEST COUNT
	POP	B			; (BC) = COUNT FOR THIS COPY
	XTHL				; (HL) = TARGET ADDR, ((SP)) = REMAINING
$REAB7	LDAX	D
	MOV	M,A
	INX	D
	INX	H
	DCX	B
	MOV	A,B
	ORA	C
	JNZ	$REAB7			; MORE TO GO
	XCHG
	SHLD	T.PTR			; UPDATE POINTER
	POP	B			; (BC) = REMAINING COUNT
	JMP	$REAB2			; SEE IF MORE IN BUFFER

*	READ COMPLETE.
*
*	(PSW) = COMPLETION FLAGS

$REAB8	POP	D			; RESTORE TARGET ADDRESS
	POP	H
	JMP	CTB			; COPY TEMP POINTERS BACK TO BLOCK, EXIT
