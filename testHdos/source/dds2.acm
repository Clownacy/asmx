	SPACE	4,10
**	DDS	-  Decode Device Specification
*
*	DDS decodes the device specification into:
*		DEVNAME	= device name
*		UNIT	= binary unit number
*
*
*	ENTRY:	DE	=  default block
*		HL	=  device specification
*
*	EXIT:	HL	=  HL advanced past the device specification
*
*	USES:	ALL
*

DDS.	CALL	DDS
	RC
	MOV	A,M
	INX	H
	ANA	A
	MVI	A,EC.IDN		; ASSUME ILLEGAL DEVICE NAME
	STC
	RNZ				; FORCE ZERO BYTE TERMINATION

	ANA	A			; CLEAR ERROR FLAG
	RET

DDS	EQU	*

*	Initialize defaults

	PUSH	H
	LXI	H,DEVNAME
	LXI	B,3
	CALL	$MOVE			; INITIALIZE DEVICE SPECIFICATION
	POP	H			; IGNORE DEFAULT EXTENSION
	XRA	A
	STA	UNIT			; INITIALIZE UNIT
	MVI	A,'0'
	STA	DEVNAME+2		; INITIALIZE ASCII UNIT

*	GET THE REAL DEVICE SPECIFICATION

	LXI	D,DEVNAME
	CALL	$SOB
	MVI	B,2
	MOV	A,M
	ANA	A
	RZ				; ACCEPT DEFAULT

DDS1	MOV	A,M
	CALL	$MCU
	CALL	ALP
	JC	DDS5			; INVALID DEVICE SPECIFICATION
	STAX	D
	INX	H
	INX	D
	DCR	B
	JNZ	DDS1

DDS2	MOV	A,M
	CPI	':'
	JZ	DDS3			; NO UNIT SPECIFIED, ASSUME 0

	CALL	UNUM
	JC	DDS5			; INVALID DEVICE SPECIFICATION
	STAX	D			; SAVE ASCII
	INX	D
	INX	H
	SUI	'0'
	STA	UNIT			; SET UP UNIT

DDS3	MOV	A,M
	INX	H
	CPI	':'
	RZ				; VALID TERMINATION WITH ':'

DDS5	MVI	A,EC.IDN		; ILLEGAL DEVICE NAME
	STC
	RET
