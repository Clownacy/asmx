	SPACE	4,10
**	$FOPEx - OPEN FILE BLOCK FOR I/O
*
*	$FOPEx IS CALLED BEFORE ANY I/O IS DONE VIA A
*	FILE BLOCK. $FOPEx SETS UP THE FILE BLOCK, AND OPENS
*	THE FILE VIA *HDOS*.
*
*	ENTRY	(DE) = ADDRESS OF DEFAULT BLOCK
*		(HL) = ADDRESS OF FILE BLOCK
*	EXIT	TO $FERROR IF ERROR
*		TO CALLER IF OK
*	USES	A,F,B,C,D,E

$FOPER	CALL	$FOPER.
	RNC
	JMP	$FERROR 		; IN ERROR

$FOPEW	CALL	$FOPEW.
	RNC
	JMP	$FERROR 		; IN ERROR

$FOPEU	CALL	$FOPEU.
	RNC
	JMP	$FERROR 		; IN ERROR

$FOPER. MVI	A,FT.OR 		; FILE TYPE OF OPEN FOR READ
	DB	001Q			; LXI,B TO SKIP NEXT MVI

$FOPEW. MVI	A,FT.OW 		; OPEN FOR WRITE
	DB	001Q			; LXI,B TO SKIP NEXT MVI

$FOPEU.	MVI	A,FT.OR+FT.OW

*	(A) = FILE FLAGS

	PUSH	H			; SAVE FILE BLOCK ADDRESS
	PUSH	PSW			; SAVE NEW FLAGS
	ERRNZ	FB.CHA
	MOV	B,M			; (B) = CHANNEL NUMBER
	PUSH	B			; SAVE CHANNEL NUMBER
	ERRNZ	FB.FLG-FB.CHA-1
	INX	H
	MOV	C,A			; (C) = NEW FILE FLAGS
	MOV	A,M			; (A) = CURRENT TYPE
	ANA	A
	MOV	A,C			; (A) = NEW FLAGS TO BE SET
	JZ	$FOPE1			; NOT ALREADY OPEN

*	ALREADY OPEN. SQUACK

	POP	B			; RESTORE (BC)
	POP	PSW			; DISCARD NEW FLAGS
	POP	H			; (HL) = FB ADDRESS
	MVI	A,EC.FAO		; FILE ALREADY OPEN
	STC
	RET

	ERRNZ	FB.FWA-FB.FLG-1
$FOPE1	INX	H			; (HL) = #FB.FWA
	MOV	C,M
	INX	H
	MOV	B,M			; (BC) = FB.FWA
	INX	H
	ERRNZ	FB.PTR-FB.FWA-2
	MOV	M,C			; SET FB.PTR = FB.FWA
	INX	H
	MOV	M,B
	INX	H
	ERRNZ	FB.LIM-FB.PTR-2
	MOV	M,C			; SET FB.LIM = FB.FWA
	INX	H
	MOV	M,B
	INX	H
	ERRNZ	FB.NAM-FB.LIM-4
	INX	H
	INX	H			; (HL) = #FB.NAM

*	FILE BLOCK POINTERS SETUP. OPEN FILE

	PUSH	H			; SAVE NEW ADDRESS FOR NAME
	LXI	H,$FOPEB
	ANA	A						/78.10.GC/
	JZ	$FOPE2
	ERRNZ	.EXIT
	CALL	$TBLS			; FIND CODE
	MOV	A,M
$FOPE2	STA	$FOPEA			; SET SYSCALL CODE
	POP	H			; (HL) = #FB.NAM
	POP	PSW			; (A) = CHANNEL NUMBER
	SCALL	.EXIT
$FOPEA	EQU	*-1			; SYSCALL CODE
	POP	D			; (D) = NEW FLAG
	POP	H			; (HL) = FILE BLOCK ADDRESS
	RC				; EXIT IF ERROR
	INX	H
	ERRNZ	FB.FLG-1
	MOV	M,D			; SET NEW FLAGS
	DCX	H			; RESTORE (HL)
	RET

$FOPEB	DB	FT.OR,.OPENR		; TABLE OF SYSCALL CODES
	DB	FT.OW,.OPENW
	DB	FT.OR+FT.OW,.OPENU
	DB	0			; SHOULD NOT OCCUR
