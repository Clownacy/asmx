	SPACE	4,10
**	DFD - DECODE FILE DESCRIPTION.
*
*	DFD CRACKS AN ALPHANUMERIC FILE DESCRIPTION, OF THE FORM
*
*	DEV:NAME.EXT
*
*	ENTRY	(DE) = POINT TO DEFAULT BLOCK
*		(HL) = POINTER TO TEXT
*	EXIT	'C' SET IF ERROR
*		 (A) = ERROR CODE
*		'C' CLEAR IF OK
*		 (HL) = POINTS PAST FILE NAME
*		 'Z' SET IF NULL NAME
*		 'Z' CLEAR IF NON-NULL
*		  AIO.DIR.NAM = NAME
*		  AIO.DIR.EXT = EXTENSION
*		  AIO.DEV = DEVICE CODE
*		  AIO.UNI = UNIT NUMBER (ASCII DIGIT)
*	USES	ALL

DFD	PUSH	H

*	SET DEFAULTS IN AIO.xxx

	LXI	H,AIO.DEV
	LXI	B,3
	CALL	$MOVE			; SET DEFALUT DEVICE
	LXI	B,3
	LXI	H,AIO.DIR+DIR.EXT
	CALL	$MOVE			; SET DEFAULT EXTENSION
	POP	H
	CALL	$SOB			; SKIP BLANKS
	CALL	$MCU			; MAP CHARACTER TO UPPER CASE
	MVI	B,0
	CPI	'.'
	JE	DFD1			; HAVE NAME
	CPI	'A'
	JC	DFD4			; NOT NAME
	CPI	'Z'+1
	JNC	DFD4			; NOT NAME

*	HAVE ALPHA STRING. CRACK IT

DFD1	CALL	DNT			; DECODE NEXT TOKEN
	JC	DFD5			; ERROR
	CPI	':'
	JNE	DFD2			; NOT DEVICE

*	HAVE EXPLICIT DEVICE

	INX	H			; SKIP ':'
	MVI	A,3
	CMP	C
	JC	DFD5			; TOO MANY CHARACTERS
	LXI	B,3
	PUSH	H			; SAVE (HL)
	LXI	H,AIO.DEV
	CALL	$MOVE			; SET EXPLICIT DEVICE
	POP	H
	CALL	DNT			; DECODE NEXT TOKEN
	JC	DFD5			; ERROR

*	DECODE NAME

DFD2	LXI	B,8			; (BC) = COUNT
	PUSH	H			; SAVE TEXT ADDR
	LXI	H,AIO.DIR+DIR.NAM
	CALL	$MOVE			; MOVE IN NAME
	POP	H
	MOV	A,M			; (A) = DELIMITER
	CPI	'.'
	JNE	DFD3			; NOT EXTENSION

*	HAVE EXPLICIT EXTENSION

	INX	H
	CALL	DNT
	JC	DFD5			; ERROR
	MVI	A,3
	CMP	C
	JC	DFD5			; TOO LONG
	LXI	B,3
	PUSH	H			; SAVE TEXT POINTER
	LXI	H,AIO.DIR+DIR.EXT
	CALL	$MOVE			; MOVE EXTENSION
	POP	H

*	DONE WITH NAME. MUST HAVE LEGIT DELIMITER

DFD3	MVI	B,1			; (B) = NAME PRESENT FLAG

*	END OF NAME. EXIT
*	(B) = 0 IF NULL, (B) <> 0 IF NON-NULL

DFD4	MOV	C,L			; (C) = #ADDR
	CALL	$SOB			; SKIP BLANKS
	MOV	A,C
	SUB	L			; SEE IF ANY BLANKS
	ANA	A			; 'Z' CLEAR IF BLANKS
	MOV	A,M			; (A) = CHARACTER
	CZ	$CFD			; CHECK FOR LEGAL DELIMITER
	RC				; ERROR
	MOV	A,B
	ANA	A			; SET 'Z' IF NULL
	RZ				; IF NULL FILE NAME

	LDA	AIO.DIR+DIR.NAM
	ANA	A							/3.0a/
	RZ				; IF NULL FILE NAME
	CPI	'A'
	JC	DFD5			; NOT ALPHA CHAR.
	CPI	'Z'+1
	JNC	DFD5			; NOT ALPHA CHAR.

	XRA	A							/3.0a/
	INR	A			; CLEAR 'Z' AND 'C' FLAG	/3.0a/

	RET

*	ERROR

DFD5	MVI	A,EC.IFN		; ILLEGAL FILE NAME
	STC
	RET
