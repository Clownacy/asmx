	SPACE	4,10
**	LDE - LOCATE DIRECTORY ENTRY.
*
*	LDE LOCATES A DIRECTORY ENTRY CORRESPONDING TO THE AIO.DIR ENTRY.
*
*	ENTRY	(BC) = NUMBER OF CHARACTERS TO MATCH ON
*	EXIT	'C' CLEAR IF FOUND
*		 AIO.DES SETUP
*		 (HL) = ADDRESS OF DIRECTORY ENTRY IN SECSCR
*		'C' SET IF NOT FOUND
*		 (A) = CODE
*	USES	ALL

LDE.	LXI	B,DIRIDL		; ENTRY FOR FULL NAME COMPARE
LDE	LDA	AIO.DIR+DIR.NAM
	ANA	A
	MVI	A,EC.FNR		; ASSUME FILE NAME MISSING
	STC
	RZ				; FILE NAME REQUIRED
	CALL	FDB			; FIND DIRECTORY BLOCK SECTOR NUMBER

**	ENTRY FOR (HL) = SECTOR NUMBER TO START WITH

LDE..	PUSH	B			; SAVE COUNT
	LXI	B,512
	XCHG
	LHLD	S.SCR
	XCHG				; DE = SECTOR SCRATCH
	SHLD	AIO.DES 		; ASSUME WILL FIND IN THIS BLOCK
	CALL	DREAD			; READ FRM DEVICE
	POP	B			; RESTORE (BC)
	RC				; RETURN IF ERROR

*	SCAN SECTOR FOR INFO

	LHLD	S.SCR
	ERRNZ	DIS.ENT

*	COMPARE

LDE3	LXI	D,AIO.DIR
	MOV	A,M
	ERRNZ	DF.EMP-377Q
	INR	A
	JZ	LDE5			; EMPTY SPOT
	ERRNZ	DF.CLR-376Q
	INR	A
	JZ	LDE6			; NO MORE FILES IN DIRECTORY
	PUSH	B			; SAVE COPY OF (BC)
	PUSH	H			; SAVE ADDRESS
	CALL	$COMP			; COMPARE
	POP	H
	POP	B			; (BC) = COMPARE COUNT
	RE				; GOT MATCH
LDE5	LXI	D,DIRELEN		; MISSED, SCAN TO NEXT ENTRY
	DAD	D
	MOV	A,M
	ANA	A
	JNZ	LDE3			; MORE IN SECTOR

*	DIDNT FIND IT IN THIS SECTOR, TRY NEXT

	LHLD	S.SCR
	CALL	$INDL
	DW	DIS.LNK
	XCHG				; HL = NEXT DIRECTORY SECTOR
	SHLD	AIO.DES 		; SET POSSIBLE SECTOR INDEX
	MOV	A,H
	ORA	L
	JNZ	LDE..			; HAVE MORE SECTORS
LDE6	MVI	A,EC.FNF		; FILE NOT FOUND
	STC
	RET
