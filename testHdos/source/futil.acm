	SPACE	4,10
**	$FUTIL - UTILITY ROUTINES FOR FILE BLOCK ROUTINES.

**	CBT - COPY BLOCK POINTERS TO TEMP CELLS.
*
*	ENTRY	(HL) = FILE BLOK FWA
*	EXIT	NONE
*	USES	A,F,H,L

CBT	PUSH	D
	PUSH	B			; SAVE REGISTERS
	ERRNZ	TLEN-10 		; ASSUME 10 BYTES TO MOVE
	LXI	D,T.CHA 		; (DE) = TARGET FOR MOVE
	MVI	B,10/2
CBT1	MOV	A,M			; COPY FILE BUFFER INTO WORK AREA
	STAX	D
	INX	H
	INX	D
	MOV	A,M
	STAX	D
	INX	H
	INX	D
	DCR	B
	JNZ	CBT1			; MORE TO GO
	POP	B
	POP	D			; (DE) = DATA TARGET ADDRESS
	RET


**	CTB - COPY TEMP CELLS BACK TO FILE BLOCK.
*
*	ENTRY	(HL) = FILE BLOCK ADDRESS
*	EXIT	NONE
*	USES	NONE

CTB	PUSH	PSW
	PUSH	D
	PUSH	B
	PUSH	H			; SAVE REGISTERS
	MVI	B,8/2
	LXI	D,T.CHA
CTB1	LDAX	D
	MOV	M,A
	INX	D
	INX	H
	LDAX	D
	MOV	M,A
	INX	D
	INX	H
	DCR	B
	JNZ	CTB1			; RESTORE FILE BUFFER VALUES
	POP	H
	POP	B
	POP	D
	POP	PSW
	RET
	SPACE	4,10
**	$FFB - FILL FILE BUFFER.
*
*	$FFB FILLS THE FILE BUFFER BY READING FROM THE FILE.
*
*	ENTRY	NONE
*	EXIT	'C' SET IF READ INCOMPLETE
*		 (A) = ERROR CODE
*		'C' CLEAR IF READ COMPLETEE
*		 DATA IN BUFFER
*	USES	A,F,D,E,H,L

$FFB	LDA	EOFFLG
	RAR
	RC				; EOF

*	CAN READ MORE. DO SO

	PUSH	B			; SAVE COUNT
	LHLD	T.FWA
	SHLD	T.PTR			; CLEAR REMOVAL POINTER
	XCHG
	LHLD	T.LWA
	SHLD	T.LIM			; SET DATA LIMIT
	MOV	A,L
	SUB	E
	MOV	C,A
	MOV	A,H
	SBB	D
	MOV	B,A			; (BC) = ROOM IN BUFFER
	LDA	T.CHA
	SCALL	.READ			; READ BUFFER
	MOV	D,B			; (D) = SECTORS UNREAD
	POP	B			; (BC) = DESIRED COUNT
	RNC				; GOT THE DATA

*	ERROR ON READ. SEE IF EOF

	RAL
	STA	EOFFLG			; SET EOF, WE HOPE
	CPI	EC.EOF*2+1
	RAR
	RNE				; IS NOT EOF, RETURN NOW!
	LDA	T.LIM+1
	SUB	D
	STA	T.LIM+1 		; SET AMOUNT OF DATA WE DID GET
	ANA	A
	RET				; EXIT WITH DATA


**	TEMP CELLS TO HOLD FILE BLOCK POINTERS DURING I/O

	ERRNZ	FB.CHA
T.CHA	DB	0			; CHANNEL NUMBER
	ERRNZ	*-T.CHA-FB.FLG
T.FLG	DB	0			; FLAG BYTE
	ERRNZ	*-T.CHA-FB.FWA
T.FWA	DW	0
	ERRNZ	*-T.CHA-FB.PTR
T.PTR	DW	0
	ERRNZ	*-T.CHA-FB.LIM
T.LIM	DW	0
	ERRNZ	*-T.CHA-FB.LWA
T.LWA	DW	0
TLEN	EQU	*-T.CHA 		; LENGTH OF TEMP CELLS

EOFFLG	DB	0
