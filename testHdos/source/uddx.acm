	SPACE	4,10
**	$UDDX - UNPACK DECIMAL DIGITS.					/3.0/
*
*	UDDX CONVERTS A 16 BIT VALUE INTO A SPECIFIED NUMBER OF DECIMAL
*	DIGITS.
*
*	$UDDN = PAD WITH NULLS
*
*	$UDDS = PAD WITH SPACES
*
*	ENTRY	(B,C) = ADDRESS VALUE
*		(A) = DIGIT COUNT
*		(H,L) = MEMORY ADDRESS
*	EXIT	(HL) = (HL) + (A)
*	USES	ALL

$UDDN	EQU	*

	MOV	D,A		SAVE (A)
	XRA	A		(A) = NUL
	JMP	$UDDX

$UDDS	EQU	*

	MOV	D,A		SAVE (A)
	MVI	A,' '		(A) = SPACE

$UDDX	STA	UDDPAD		SET PAD CHARACTER
	MOV	A,D		RESTORE (A)
	CALL	$DADA
	PUSH	H		SAVE FINAL (H,L) VALUE

UDDX1	PUSH	PSW
	PUSH	H
	LXI	D,10
	CALL	$DU66		(H,L) = VALUE/10
	MOV	B,H
	MOV	C,L		(BC) = QUOTIENT
	POP	H
	MVI	A,'0'
	ADD	E		ADD REMAINDER
	DCX	H
	MOV	M,A		STORE DIGIT
	MOV	A,B
	ORA	C
	JZ	UDDX2		ALL ZEROS
	POP	PSW
	DCR	A
	JNZ	UDDX1		IF MORE TO GO

*	ALL DONE. EXIT

UDDX1.5	POP	H		RESTORE H
	RET			RETURN

*	DIGITS LEADING THIS ONE ARE ZERO. STORE NULLS INSTEAD.

UDDX2	POP	PSW
UDDX3	DCR	A
	JE	UDDX1.5 	ALL DONE
	DCX	H
	MVI	M,0
UDDPAD	EQU	*-1
	JMP	UDDX3
