	SPACE	4,10
**	DFSS - DECODE FLAG SUB-SWITCHES 			/3.0a/
*
*	DFSS IS CALLED TO DECODE THE SPECIFIED FLAG SUB-SWITCHES FOR
*	THE /FLAG: AND THE /NOFLAG: SWITCHES.
*
*	DFSS. IS CALLED TO DECODE THE SPECIFIED USER SUB-SWITCHES FOR
*	THE /USER: AND THE /NOUSER: SWITCHES.
*
*	THE SUB-SWITCHES ARE ANALYZED AND REPLACED WITH BLANKS
*
*	ENTRY:	(HL) = ADDRESS OF ':ff..'
*	EXIT:	'C' CLEAR IF OK
*		 (A) = BITS SET FOR EACH SPECIFIED OPTION
*		'C' SET IF ERROR
*	USES:	ALL

	IF	USER
DFSS	PUSH	H
	LXI	H,DFSSA 	FLAGS TABLE
	JMP	DFSS..

DFSS.	PUSH	H
	LXI	H,DFSSU 	USER AREA TABLE
DFSS..	SHLD	DFTT
	POP	H
	ELSE
DFSS	EQU	*
	ENDIF

	MOV	A,M		(A) = SUPPOSED ':'
	CPI	':'		CHECK IT OUT
	STC
	RNE			NOT ':'
	XCHG			(DE) = ADDRESS
	MVI	B,0

*	DECODE NEXT SUB-SWITCH

DFSS1	MVI	A,' '
	STAX	D		CLEAR THAT ONE
	INX	D
	LDAX	D
	CPI	' '
	JE	DFSS1		SKIP
	CPI	'/'		NEXT SWITCH
	JE	DFSS2		DONE
	CPI	','		DELIMITER
	JE	DFSS2		DONE
	ANA	A		END OF LINE
	JE	DFSS2		DONE

*	MUST BE A SUB-SWITCH

	LXI	H,DFSSA 	OPTION TABLE
DFTT	EQU	*-2
	CALL	$TBLS
	STC
	RNZ			NOT A GOOD OPTION
	MOV	A,M
	ORA	B		SET SUB-SWITCH
	MOV	B,A
	JMP	DFSS1		GET ANOTHER

*	ALL DONE

DFSS2	MOV	A,B
	ANA	A
	STC
	RZ			NONE FOUND, ERROR
	ANA	A		CLEAR CARRY
	RET			GO BACK WITH ALL OK

DFSSA	EQU	*		FLAG TABLE
	DB	'S',DIF.SYS
	DB	'L',DIF.LOC
	DB	'W',DIF.WP
DFSSC	DB	'C',DIF.CNT
	DB	'A',DIF.ARC
	DB	'B',DIF.BAD
	DB	'D',DIF.DL
	DB	'U',DIF.USR
	DB	'*',11101111B	ALL BUT 'C'
	DB	0,0

	IF	USER
DFSSU	EQU	*		USER AREA TABLE
	DB	'0',10000000B
	DB	'1',01000000B
	DB	'2',00100000B
	DB	'3',00010000B
	DB	'4',00001000B
	DB	'5',00000100B
	DB	'6',00000010B
	DB	'7',00000001B
	DB	'*',01111111B	ALL BUT 0
	DB	0,0
	ENDIF
