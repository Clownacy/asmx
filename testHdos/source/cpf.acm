	SPACE	4,10
**	$CPF - COPY FILE NAME.
*
*	$CPF COPIES A FILE NAME FROM ONE LOCATION TO ANOTHER.
*
*	THE CHARACTERS ARE COPIED UNTIL A DELIMITER (',', ' ', '=', OR 00)
*	IS FOUND.
*
*	THE FILE NAME IS THEN TERMINATED WITH A 00 BYTE.
*
*	ENTRY:	(DE) = FROM ADDRESS
*		(HL) = TO ADDRESS
*	EXIT:	'C' CLEAR IF OK
*		 (DE) = ADVANCED PAST NAME AND DELIMITER
*		 (HL) POINTS TO 00 BYTE OF DESTINATION
*		 (A) = DELIMITER
*		'C' SET IF ERROR
*	USES:	ALL

$CPF	MVI	B,FB.NAML+1		; SET MAXIMUM LENGTH
$CPF1	LDAX	D
	ANA	A
	JZ	$CPF2			; END
	INX	D
	CPI	','
	JE	$CPF2
	CPI	'='
	JE	$CPF2
	CPI	' '
	JE	$CPF2			; IS BLANK
	MOV	M,A			; COPY
	INX	H
	DCR	B
	JNZ	$CPF1			; IF MORE GO TO
	STC				; OVERFLOW OF AREA
	RET

*	DONE.

$CPF2	MVI	M,0			; TERMINATE
	RET
