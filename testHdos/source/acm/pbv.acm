PBV	SPACE	4,10
**	PBV - PROCESS BYTE VALUES
*
*	PROCESS BYTE VALUE OPTIONS.  THE FORMAT FOR TABLE ENTRIES
*	IS:
*
*		<DEFAULT RADIX>,<MIN.>,<MAX.>,<LOW. ADDR.>,<HIGH ADDR.>
*
*
*	ENTRY:	(BC)	= NEXT TEXT CHARACTER ADDRESS
*		(HL)	= TABLE VECTOR ADDRESS
*
*	EXIT:	(BC)	= UPDATED
*		'C' CLEAR IF OK
*		'C' SET IF ERROR
*		 (A) = ERROR CODE
*
*	USES:	ALL
*

PBV	MOV	A,M		(A)  = DEFAULT RADIX
	INX	H
	PUSH	H		SAVE VECTOR ADDRESS
	CALL	DNF		(HL) = VALUE
	JC	PBV2
	MOV	A,H
	ANA	A
	JNZ	PBV2
	POP	D
	XCHG			(HL) = NEXT TABLE ADDRESS, (E) = VALUE
	PUSH	B		SAVE TEXT POINTER
	MOV	B,M		(B)  = MIN.
	INX	H
	MOV	C,M		(C)  = MAX.
	INX	H
	CALL	$HLIHL		(HL) = BYTE  VALUE ADDRESS
	MOV	A,E
	CMP	B
	JC	PBV3		(A) < MIN.
	INR	C
	JZ	PBV1		IGNORE COMPARE IF C=377Q
	CMP	C
	JNC	PBV3		(A) >= MAX. + 1
PBV1	POP	B		RESTORE TEXT ADDR.
	MOV	M,A		PATCH IT
	XRA	A		CLEAR CARRY
	RET

PBV2	POP	H
	PUSH	B
PBV3	POP	B
	MVI	A,EC.ILV	ILLEGAL VALUE SPECIFICATION
	STC
	RET
