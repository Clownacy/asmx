	SPACE	4,10
**	$DDD - DECODE DECIMAL DIGITS.
*
*	$DDD DECODES A STRING OF DECIMAL DIGITS INTO A DECIMAL INTEGER.
*
*	THE CHARACTERS ARE TAKEN OUT OF MEMORY. CONVERSION STOPS WITH THE
*	FIRST NON-DIGIT CHARACTER FOUND.
*
*	ENTRY	(HL) = ADDRESS OF CHARACTERS
*	EXIT	'C' CLEAR IF OK
*		 (DE) = NUMBER
*		 (HL) = INDEX OF FIRST NON-DIGIT ENCOUNTERED
*		'C' SET IF ERROR
*	USES	A,F,D,E,H,L

$DDD	LXI	D,0			; (DE) = ACCUM

$DDD1	MOV	A,M
	SUI	'0'
	CMC
	RNC				; TOO SMALL
	CPI	10
	RNC				; TOO LARGE
	INX	H			; ADVANCE ADDRESS
	PUSH	H			; SAVE (HL)
	CALL	$MU10			; (HL) = ACCUM*10
	XCHG				; (DE) = ACCUM
	POP	H			; (HL) = ADDRESS OF STRING
	RC				; OVERFLOW
	ADD	E
	MOV	E,A
	MVI	A,0
	ADC	D
	MOV	D,A
	JNC	$DDD1			; NOT OVERFLOW
	RET
