	SPACE	4,10
**	$TDD - TYPE DECIMAL DIGITS.
*
*	$TDD TYPES A 16 BIT VALUE AS 1 TO 5 DECIMAL DIGITS.
*
*	ENTRY	(D,E) = VALUE
*		(A) = DIGIT COUNT
*	EXIT	VALUE TYPED.
*	USES	A,B,C,F

$TDD.	MVI	A,5
$TDD	PUSH	H
TDD1	PUSH	PSW
	LXI	H,TDDA-2
	RLC				; (A) = DIGIT NUMBER*2
	CALL	$DADA.
	MOV	A,M
	INX	H
	MOV	H,M
	MOV	L,A			; (HL) = MULTIPLE OF 10
	XCHG				; (DE) = DEVISOR, (HL) = VALUE
	MVI	A,377Q
TDD2	DAD	D
	INR	A
	JC	TDD2			; IF MORE TO GO
	ADI	'0'
	CALL	$TYPC.			; TYPE DIGIT
	MOV	A,L
	SUB	E
	MOV	E,A			; REMOVE EXTRA SUBTRACTION
	MOV	A,H
	SBB	D
	MOV	D,A
	POP	PSW
	DCR	A
	JNZ	TDD1			; IF MORE DIGITS
	POP	H
	RET

TDDA	DW	-1
	DW	-10
	DW	-100
	DW	-1000
	DW	-10000
