**	CPA - CHECK FOR PENDING ABORT.
*
*	CPA IS CALLED WHEN A CONSOLE ABORT MAY BE PROCESSED.
*	IF THE SYSTEM IS READY, AND AND ABORT IS PENDING, PROCESS IT.
*
*	CPA SHOULD BE CALLED WITH INTERRUPTS DISABLED, SO THAT
*	ANOTHER INTERRUPT CHARACTER CANNOT OCCUR DURING CPA PROCESSING.
*	THIS GUARANTEES THAT A USER PROGRAM WILL BE 'INTERRUPTED' WITH
*	THE PROGRAM COUNTER IN THE USER CODE, NEVER IN HDOS CODE.
*
*	UPON ENTRY TO THE USER INTERRUPT ROUTINE,
*
*	((SP)+0) = RETURN ADDRESS (IF USER WISHES TO RESUME NORMAL PROCESSING)
*	((SP)+2) = USER PSW
*	((SP)+4) = USER INTERRUPTED ADDRESS
*
*	THE USER REGISTER VALUES FOR B,C,D,E,H, AND L ARE STILL
*	IN THE REGISTERS.
*
*	ENTRY	((SP)+0) = RETURN ADDRESS
*		((SP)+2) = USER PSW
*		((SP)+4) = USER INTERRUPTED ADDRESS
*	EXIT	TO *RET* IF NONE, OR DISABLED
*		TO PROCESSOR IF READY AND OK
*	USES	A,F

CPA	equ	*

	IFT	HOSRES
        LDA     SYSMODE
	ELSE
	PUSH	H
	LHLD	S.DLINK
	ERRNZ	M.SYSM
	MOV	A,M
	POP	H
	ENDIF

	ANA	A
	RNZ				; IN SCALL MODE

*	WILL ALLOW PROCESSING

	LDA	S.CAADR+1		; (A) = HIGH BYTE ABORT ADDRESS
	ANA	A
	RZ				; NO ABORT PENDING

*	HAVE ABORT. PROCESS IT

	PUSH	H
	LHLD	S.CAADR 		; (HL) = ADDRESS FOR JUMP
	XRA	A
	STA	S.CAADR+1		; CLEAR
	INR	A			; SET (A) <>0
	XTHL				; RESTORE (HL), SET PROCESSOR
	EI
	RET				; ENTER ROUTINE
