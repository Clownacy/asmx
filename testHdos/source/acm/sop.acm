SOP	SPACE	4,30
**	SOP - SET OPTIONS
*
*	PROCESS OPTION SET VIA OPTION TABLE, AND PROCESSOR TABLE.
*
*	OPTION TABLE FORMAT:  P(1)=PROCESSOR INDEX, P(2,...,N)=PARAMETERS
*
*		DW	<END OF TABLE>
*		DB	N
*		DB	'<SEARCH STRIN','G>'+200Q,P,P(1),...,P(N)
*		.	.			  .
*		.	.			  .
*	<EOT>	DB	0			END OF TABLE
*
*	PROCESSOR TABLE FORMAT:
*
*		DW	PROC.0
*		DW	PROC.1
*		.	.
*		.	.
*		DW	PROC.N
*
*
*	ENTRY:	(BC)	= LINE POINTER
*		(DE)	= JUMP TABLE ADDRESS
*		(HL)	= OPTION TABLE ADDRESS
*
*	EXIT:	(RET)	= TO PROCESSOR IF NO ERROR
*			= 'C' SET IF ERROR
*			   (A) = ERROR CODE
*		(BC)	= LINE POINTER UPDATED
*		(HL)	= ADDRESS OF NEXT AVAILABLE DATA BYTE
*	
*	USES:	ALL


SOP	PUSH	D
	PUSH	H
	CALL	DCS		(DE) = FWA, (HL) = LWA
	JZ	SOP1
	MOV	A,M
	ORI	200Q
	MOV	M,A
	POP	H		(HL) = OPT. TABLE ADDR.
	CALL	$FST
	JNZ	SOP2
	XCHG			(DE) = ADDR. OF FIRST DATA BYTE
	POP	H		(HL) = JUMP TABLE ADDR.
	LDAX	D		(A)  = PROCESSOR INDEX	
	RLC			X 2
	INX	D
	CALL	$DADA.
	CALL	$HLIHL		(HL) = PROCESSOR ADDRESS
	PUSH	H
	XCHG			(HL) = NEXT DATA BYTE ADDRESS
	RET			ENTER PROCESSOR

SOP1	POP	H
SOP2	POP	D
	MVI	A,EC.ILO	ILLEGAL OPTION SPECIFICATION
	STC
	RET
