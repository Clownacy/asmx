TYPLN	SPACE	4,10
**	$TYPLN - TYPE LINE.
*
*	$TYPLN IS CALLED TO TYPE A LINE OF TEXT. ZERO BYTES ARE
*	TAKEN AS CRLF (WITH THE PROPER PADDING)
*
*	CALL	$TYPLN
*	DB	N		BYTE COUNT OF FOLLOWING MESSAGE
*	DB	'N-CHARACTER MESSAGE'
*
*	ENTRY	(RET) = TEXT COUNT
*		(RET)+1 - (RET)+N = TEXT
*	EXIT	TO (RET)+N+1
*	USES	A,F
*

$TYPLN. XTHL				; (H,L) = COUNT ADDRESS
	MOV	A,M			; (A) = COUNT
	INX	H			; (H,L)  = TEXT ADDRESS
	PUSH	H			; SAVE TEXT FWA
	CALL	$DADA			; CALCULATE RETURN ADDRESS
	XTHL				; (HL) = TEXT ADDRE
	CALL	$TYPL.			; OUTPUT LINE
	POP	H			; (HL) = RETURN ADDRESS
	XTHL				; RESTORE (HL), SET RETURN ADDRESS
	RET
	SPACE	4,10
**	$TYPL. - TYPE LINE.
*
*	ENTRY	(HL) = ADDRESS
*		(A) = COUNT
*	EXIT	NONE
*	USES	A,F,H,L

$TYPL.	EQU	*

	ANA	A
	RZ				; NOTHING TO TYPE
	PUSH	PSW			; SAVE COUNT
	MOV	A,M			; (A) = CHARACTER
	INX	H
	ANA	A
	IF	$CMP$			; IF HAVE COMPRESSED SPACES
	JM	TPL2			; IS COMPRESSED SPACE
	ENDIF
	CZ	$CRLF
	CALL	$TYPC.			; TYPE CHARACTER
TPL1	POP	PSW
	DCR	A
	JNZ	$TYPL.
	RET

	IF	$CMP$			; IF COMPRESSED TEXT

*	HAVE COMPRESSED SPACE.

TPL2	DCR	A
	CP	$TYPCH			; TYPE 00 IF CHARACTER WAS 200Q
	DB	0
	ANA	A			; SET CODES
TPL3	JP	TPL1			; ALL EXPANDED
	PUSH	PSW			; SAVE COUNT
	CALL	$TYPCH
	DB	' '
	POP	PSW
	DCR	A
	JMP	TPL3
	ENDIF
