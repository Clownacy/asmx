***	PCL - PASS COMMAND LINE.
*
*	PCL PASSES A COMMAND LINE INTO THE STACK, FOR USE BY THE PROGRAM
*	WHICH WILL BE 'LINK'ED TO.
*
*	THE N BYTES ARE PUT IN THE STACK STARTING AT 'STACK-N' TO 'STACK-1'
*
*	** NOTE **
*
*	THIS ROUTINE PLAYS WITH THE STACK.  IT IS ENTERED VIA A CALL, BUT
*	IT THEN EMPTIES THE STACK TO SET UP THE COMMAND LINE.  THUS PCL'S
*	CALLER MUST NOT TRY TO RETURN TO IT'S CALLER.
*
*	Entry:	(BC) = LINE ADDRESS
*	Exit:	TO CALLER
*		(SP) = #STACK - N
*	Uses:	ALL

PCL	POP	H			; (HL) = RETURN ADDRESS
	DI				; NO INTERRUPTIONS WHILE MESSING W/ STACK
	SHLD	PCLA			; SET RETURN ADDRESS

	MOV	H,B
	MOV	L,C
	CALL	$MLU			; Map line to upper case

	LXI	D,STACK
	CALL	FEC			; FIND END OF COMMAND
	LDAX	B
	ANA	A
	JZ	PCL2			; HAVE NO LINE TO PASS

*	GOT A LINE, MOVE INTO STACK AREA.

PCL1	MOV	A,M
	DCX	D
	STAX	D			; STORE
	MOV	A,L
	CMP	C
	DCX	H
	JNE	PCL1			; MORE TO GO

PCL2	XCHG
	SPHL				; SET (SP) BELOW DATA
	EI
	JMP	*-*			; EXIT
PCLA	EQU	*-2
