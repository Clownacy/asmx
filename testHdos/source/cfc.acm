	SPACE	4,10
**	CFC - CHECK FILE CONFLICTS.
*
*	CFC CHECKS TO SEE IF A FILE TO BE OPENED CONFLICTS WITH ANOTHER
*	FILE CURRENTLY OPEN.
*
*	A FILE OPEN FOR READ MAY HAVE MANY READERS,
*	A FILE OPEN FOR WRITE (OR UPDATE) MAY ONLY HAVE ONE
*	USER.
*
*	ENTRY	NONE
*	EXIT	(A) = LOGICAL OR OF IOC.FLG OF ALL FILES WITH
*		 THE SAME NAME AND DEVICE
*	USES	ALL

CFC	MVI	B,0			; (B) = FLAG ACCUMULATOR
	LHLD	S.CFWA			; (HL) = FWA CHANNEL TABLE

*	SEE IF THIS CHANNEL USES SAME NAME AND DEVICE.

CFC1	MOV	E,M
	INX	H
	MOV	D,M
	INX	H
	ERRNZ	IOC.DDA-IOC.LNK-2	; (HL) = #IOC.DDA
	PUSH	D			; SAVE NEXT ADDRESS
	LXI	D,AIO.DDA
	ERRNZ	IOC.DDA-2		; (HL) = #IOC.DDA OF ENTRY
	MVI	C,2
	CALL	$COMP			; COMPARE
	JNE	CFC2			; NO MATCH
	ERRNZ	IOC.FLG-IOC.DDA-2	; (HL) = IOC.FLG OF ENTRY
	PUSH	H			; SAVE
	LXI	D,IOC.UNI-IOC.DDA-2
	ERRNZ	IOC.DIR-IOC.UNI-1
	ERRNZ	AIO.DIR-AIO.UNI-1	; COMPARE UNIT #'S AND NAMES
	DAD	D			; (HL) = IOC.DIR POINTER
	LXI	D,AIO.UNI
	MVI	C,DIRIDL+1
	CALL	$COMP			; COMPARE UNIT NUMBERS AND NAMES
	POP	H
	JNE	CFC2			; NO MATCH

*	HAVE MATCH

	MOV	A,B
	ORA	M			; OR FLAG
	MOV	B,A

CFC2	POP	H			; (HL) = NEXT ADDRESS
	MOV	A,H
	ORA	L			; SEE IF MORE CHANNELS
	JNZ	CFC1			; MORE
	MOV	A,B			; (A) = FLAGS
	RET
