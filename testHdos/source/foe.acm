	SPACE	4,10
**	FOE - FIND OPEN DIRECTORY ENTRY.
*
*	FOE IS CALLED TO LOCATE AN OPEN DIRECTORY ENTRY.
*
*	ENTRY	AIO.XXX SETUP
*	EXIT	'C' CLEAR, ENTRY FOUND
*		 SEC.SCR = DIRECTORY SECTOR
*		 (HL) = ADDRESS OF SEC.SCR LOCATION
*		'C' SET, DIRECTORY FULL
*		 (A) = ERROR CODE
*	USES	ALL

FOE	CALL	FDB			; FIND DIRECTORY BLOCK

*	READ ANOTHER SECTOR

FOE1	LXI	B,512
	XCHG
	LHLD	S.SCR
	XCHG				; DE = SECTOR SCRATCH ADDRESS
	CALL	DREAD			; READ SECTOR
	RC				; RETURN IF ERROR

*	SCAN SECTOR FOR EMPTY SPOT

	LHLD	S.SCR
	ERRNZ	DIS.ENT

FOE2	MOV	A,M
	ANA	A
	RM				; GOT EMPTY SPOT
	JZ	FOE3			; END OF CONTENTS
	LXI	D,DIRELEN
	DAD	D
	JMP	FOE2			; TRY NEXT ENTRY

*	SECTOR IS FULL UP. READ ANOTHER

FOE3	LHLD	S.SCR
	CALL	$INDL
	DW	DIS.LNK
	XCHG				; HL = NEXT SECTOR OF DIR.
	MOV	A,H
	ORA	L
	JNZ	FOE1			; READ ANOTHER

	MVI	A,EC.DIF		; DIRECTORY FULL
	STC				; FLAG ERROR
	RET
