***	PROMPT - SET/SHOW SYSTEM PROMPT
*
*	PRO[MPT]		- SHOW CURRENT PROMPT STRING
*	PRO[MPT] prompt string	- SET NEW PROMPT STRING
*	PRO[MPT] ~		- CLEAR PROMPT STRING (USE DEFAULT)

PROMPT	POP	H
	CALL	$SOB
	ANA	A
	JZ	PRMSHOW
	CPI	'~'
	JE	PRMCLR

*	SET NEW PROMPT STRING UNTIL LIMIT OR NULL

	XCHG
	LHLD	S.PRMT			; BUFFER
	XCHG
	MVI	C,100			; CHAR. LIMIT
PRM1	MOV	A,M
	STAX	D
	ANA	A
	JZ	SYSCMD. 		; FOUND NULL
	INX	H
	INX	D
	DCR	C
	JNZ	PRM1
	XRA	A
	STAX	D			; FORCE FINAL NULL
	RST	SYSCMD			; REACHED LIMIT

PRMSHOW CALL	$TYPTX
	DB	NL,'PROMPT ','"'+200Q
	LHLD	S.PRMT			; PRINT STRING DATA
PRM2	MOV	A,M
	ANA	A
	JZ	PAT3			; UNTIL WE FIND A NULL BYTE
	SCALL	.SCOUT
	INX	H
	JMP	PRM2

PRMCLR	XRA	A
	LHLD	S.PRMT
	MOV	M,A			; Clear PROMPT string
	RST	SYSCMD
