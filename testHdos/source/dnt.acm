	SPACE	4,10
**	DNT - DECODE NEXT TOKEN.
*
*	DNT COPIES THE NEXT ALPHANUMERIC FIELD INTO A ZERO-FILLED WORK AREA.
*	THE CHARACTERS ARE ALL MAPPED TO UPPER CASE.
*
*	ENTRY	(HL) = TEXT POINTER
*	EXIT	'C' SET IF ERROR
*		'C' CLEAR IF OK
*		(A) = DELIMTER CHARACTER
*		(HL) UPDATED TO DELIMITER CHARACTER
*		(DNTA) = STRING
*		(C) = LENGTH
*		(DE) = #DNTA
*	USES	ALL

DNT	LXI	D,DNTA
	MVI	C,9			; (C) = SIZE OF DNTA
	MOV	B,C			; (B) = MAX ALLOWED +1
	XRA	A
DNT1	STAX	D			; ZERO BUFFER
	INX	D
	DCR	C
	JNZ	DNT1
	LXI	D,DNTA

*	COPY CHARACTERS

DNT2	MOV	A,M
	CALL	$MCU			; MAP CHARACTER TO UPPER CASE
	CPI	'0'
	JC	DNT4			; NOT ALPHANUMERIC
	CPI	'9'+1
	JC	DNT3			; NUMERIC
	CPI	'A'
	JC	DNT4			; DELIMITER
	CPI	'Z'+1
	JNC	DNT4			; DELIMITER

*	HAVE GOOD CHARACTER

DNT3	STAX	D			; STORE CHAR
	INX	D
	INX	H
	INR	C			; COUNT
	DCR	B			; LIMIT DECREMENT
	JNZ	DNT2			; NOT OVERFLOW

*	OVERFLOW

	STC				; FLAG ERR
	RET

*	END OF STRING

DNT4	ANA	A			; CLEAR 'C'
	LXI	D,DNTA			; SET POINTER
	RET

DNTA	dc	9,#0			; WORK AREA
