	SPACE	3,10
**	DFA - DECODE FILE INFORMATION INTO ACTIVE CHANNEL.
*
*	IT CRACKS THE FILE NAME INTO THE AIO.DIR FIELDS, AND
*	DECODES DEVICE INFORMATION, AND LOADS THE DEVICE DRIVER.
*
*	ENTRY	(DE) = DEFAULT BLOCK ADDRESS
*		(HL) = FILE NAME IN ASCII
*	EXIT	'C' CLEAR IF OK
*		 (HL) = #AIO.FLG
*		 (A) = AIO.FLG
*		'C' SET IF ERROR
*		 (A) = ERROR CODE
*	USES	ALL
*

DFA	CALL	CAC			; CLEAR ACTIVE CHANNEL
	CALL	DFD			; DECODE FILE DESCRIPTOR
	RC				; ERROR
	SHLD	OPENHL			; SAVE POINTER
	CALL	CDU			; CHECK DEVICE UNIT
	RC				; ERROR
	CALL	LDI			; LOCATE DEVICE INFORMATION
	RC				; IF ERROR

*	GOT NAME, DEVICE INFO, ETC. POINT TO FLAG BYTE.

	LXI	H,AIO.FLG
	MOV	A,M

*	Check directory devices for null file name

	ANI	DT.DD
	MOV	A,M
	RZ				; not a directory device

	LDA	AIO.DIR+DIR.NAM
	ANA	A
	MVI	A,EC.IFN
	STC
	RZ				; was a null file name

	MOV	A,M			; restore AIO.FLG
	ANA	A			; clear carry
	RET
