	SPACE	4,10
**	ABR - AUTO BAUD RATE SELECTION.
*
*	ABR READS CHARACTERS FROM THE SYSTEM CONSOLE ACE UNTIL
*	THE CURRENT BAUD RATE IS DETERMINED.
*
*	ENTRY	NONE
*	EXIT	(HL) = BAUD RATE DIVISOR
*		ACE SETUP WITH BAUD RATE, NO INTERRUPTS
*	USES	ALL

ABR	EQU	*

*	INITIALIZE LED DISPLAY FOR PROMPT

ABR0.1	LDA	.MFLAG
	PUSH	PSW
	ORI	UO.DDU
	STA	.MFLAG
	LXI	B,9
	LXI	D,ABR.A
	LXI	H,.ALEDS
	CALL	$MOVE
	LXI	D,.ALEDS
	MVI	A,100
.HORN	EQU	002140A
	CALL	.HORN

	LXI	H,TABLE

ABR0.3	XRA	A
	OUT	SC.ACE+UR.IER
	MVI	A,UC.LOO
	OUT	SC.ACE+UR.MCR		; SET LOOP BACK
	MVI	A,UC.DLA
	OUT	SC.ACE+UR.LCR		; LINE CONTROL ACCESS
	MOV	A,M
	INX	H
	OUT	SC.ACE+UR.DLL		; DIVISOR LEAST SIGNIFICANT
	MOV	A,M
	ANI	177Q			; CLEAR STOP BITS FLAG
	OUT	SC.ACE+UR.DLM		; DIVISOR MOST SIGNIFICANT
	CMP	M			; SEE IF 2 STOP BITS
	INX	H
	MVI	A,UC.8BW		; ASSUME 8 BIT WORDS, 1 STOP
	JE	ABR0.5
	MVI	A,UC.8BW+UC.2SB 	; SET 2 STOP BITS
ABR0.5	OUT	SC.ACE+UR.LCR		; LINE CONTROL ACCESS
	MVI	A,AC.DLY
	CALL	.DLY			; WAIT FOR 8250 TO SETTLE
	IN	SC.ACE+UR.MCR
	ANI	377Q-UC.LOO
	OUT	SC.ACE+UR.MCR		; TURN OFF LOOP

*	WAIT FOR CHARACTER TO BE HIT

ABR0	IN	SC.ACE+UR.RBR		; GOBBLE OVERRUN
ABR1	IN	SC.ACE+UR.LSR
	ERRNZ	UC.OR-2
	RAR
	RAR
	JC	ABR0			; OVERRUN
	RAL
	RAL
	ANI	UC.DR+UC.PE+UC.FE
	JZ	ABR1			; NOTHING YET
	PUSH	PSW
	LDAX	D			; ECHO ' ' AS '.' ON LEDS
	ANI	01111111B		; TURN ON '.'
	STAX	D
	INX	D
	POP	PSW
	ANI	UC.FE
	JNZ	ABR3			; USER IS SLOWER THAN THIS
	IN	SC.ACE+UR.RBR		; GET DATA
	ANI	177Q			; TRIM
	CPI	' '
	JE	ABR5
	CPI	CR			; also accept CR		/3.0a/
	JE	ABR5							/3.0a/

*	USER IS FASTER THAN WE ARE. FOLLOW FASTER LINKAGE

ABR2	MOV	L,M			; FOLLOW LINK
	JMP	ABR0.3			; TRY AGAIN

*	USER IS SLOWER THAN WE ARE. READ NEXT CHARACTER

ABR3	MVI	A,110/2
	CALL	.DLY			; WAIT FOR THINGS TO SETTLE OUT
	IN	SC.ACE+UR.RBR
	IN	SC.ACE+UR.LSR
	INX	H
	JMP	ABR2

*	FOUND THE BAUD RATE. RETURN WITH ANSWERS

ABR5	LXI	D,.ALEDS		; BLANK DISPLAY
	MVI	B,9
	MVI	A,377A
ABR5.1	STAX	D
	INX	D
	DCR	B
	JNZ	ABR5.1
	DCX	H
	MOV	D,M
	DCX	H
	MOV	E,M
	XCHG				; (HL) = BAUD RATE
	IN	SC.ACE+UR.RBR		; GOBBLE THE GARBAGE
	POP	PSW
	STA	.MFLAG
	RET

*	'SPACE' for LEDS

ABR.A	DB	244Q,230Q,220Q,215Q,214Q,377Q,377Q,377Q,377Q
	SPACE	4,10
**	BAUD RATE SELECTION TREE.
*
*	Added 38400 baud						/3.0a/

.	SET	#*
	IFT	(256-.)<(14*4)
	NOTE	256-.			; fill length
	DC	256-.,#0		; go to next page if not room
	ENDIF

TABLE	EQU	*			; START OF BAUD TABLE

	DW	000060A 		; 2400 BAUD
	DB	#T9600			; USER IS FASTER
	DB	#T600			; USER IS SLOWER

*	2ND TRY GROUPS

T9600	DW	000014A 		; 9600 BAUD
	DB	#T19200 		; USER IS FASTER
	DB	#T4800			; USER IS SLOWER

T600	DW	000300A 		; 600 BAUD
	DB	#T1200			; USER IS FASTER
	DB	#T300			; USER IS SLOWER

*	3RD TRY GROUPS

T19200	DW	000006A 		; 19200 BAUD
	DB	#T38400 		; USER IS FASTER		/3.0a/
	DB	#TABLE			; USER IS SLOWER, IS ERROR

T4800	DW	000030A 		; 4800 BAUD
	DB	#T7200			; USER IS FASTER
	DB	#T3600			; USER IS SLOWER

T1200	DW	000140A 		; 1200 BAUD
	DB	#T1800			; USER IS FASTER
	DB	#TABLE			; USER IS SLOWER, IS ERROR

T300	DW	001200A 		; 300 BAUD
	DB	#TABLE			; USER IS FASTER, IS ERROR
	DB	#T110			; USER IS SLOWER

*	4TH TRY GROUPS

T38400	DW	000003A 		; 38400 baud			/3.0a/
	DB	#TABLE			; USER IS FASTER, IS ERROR	/3.0a/
	DB	#TABLE			; USER IS SLOWER, IS ERROR	/3.0a/

T7200	DW	000020A 		; 7200 BAUD
	DB	#TABLE			; USER IS FASTER, IS ERROR
	DB	#TABLE			; USER IS SLOWER, IS ERROR

T3600	DW	000040A 		; 3600 BAUD
	DB	#TABLE			; USER IS FASTER, IS ERROR
	DB	#TABLE			; USER IS SLOWER, IS ERROR

T1800	DW	000100A 		; 1800 BAUD
	DB	#TABLE			; USER IS FASTER, IS ERROR
	DB	#TABLE			; USER IS SLOWER, IS ERROR

T110	DW	204027A 		; 110 BAUD
	DB	#T150			; USER IS FASTER
	DB	#T75			; USER IS SLOWER

*	5TH TRY GROUPS

T150	DW	003000A 		; 150 BAUD
	DB	#TABLE			; USER IS FASTER, IS ERROR
	DB	#TABLE			; USER IS SLOWER, IS ERROR

T75	DW	006000A 		; 75 BAUD
	DB	#TABLE			; USER IS FASTER, IS ERROR
	DB	#TABLE			; USER IS SLOWER, IS ERROR

.	SET	*/256
	ERRNZ	TABLE/256-.		; MUST BE IN SAME PAGE
	ERRNZ	*-TABLE/4-14
