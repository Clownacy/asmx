	SPACE	3,10
**	CDS - CLEAR DIRECTORY SPACES.
*
*	CDS IS CALLED TO FLAG THE UNUSED ENTRYS AT THE
*	END OF THE DIRECTORY AS CLEAR.
*
*	WHEN A FILE IS DELETED, ITS ENTRY IS FLAGED EMPTY. CDS LOCATES
*	EMPTY SPOTS WHICH ARE AFTER THE LAST FILE IN THE DIRECTORY,
*	AND FLAGS THEM CLEAER.
*
*	ENTRY	(HL) = SECTOR NUMBER OF LAST DIRECTORY BLOCK WITH FILES
*	EXIT	NONE
*	USES	ALL

CDS	LXI	B,512
	CALL	PGT12.			; DE = SECTOR SCRATCH
	PUSH	D			; SAVE #SECSCR
	XRA	A
	ERRNZ	DC.REA
	CALL	DRIVER			; READ DIRECTORY BLOCK
	POP	D							/3.0a/
	RC				; ERROR 			/3.0a/
	MOV	H,D
	MOV	L,E			; (DE) = (HL) = #SECSCR

*	FIND LAST FILE NAME IN THIS BLOCK

CDS1	MOV	A,M
	ANA	A
	JZ	CDS3			; END OF BLOCK
	JM	CDS2			; EMPTY OR CLEAR
	MOV	D,H
	MOV	E,L			; (DE) = ADDRESS OF THAT FILE NAME

CDS2	CALL	CDS6.			; A  = DIRECTORY ENTRY LENGTH
	CALL	$DADA.
	JMP	CDS1			; TRY NEXT ONE

*	ALL EMPTY SPOTS FOLLOWING THAT LAST NAME TO BE FLAGGED CLEAR

CDS3	XCHG				; (HL) = ADDRESS OF LAST FILE ENTRY

CDS4	MOV	A,M			; (A) = ENTRY FIRST BYTE
	ANA	A
	JZ	CDS5			; END OF BLOCK
	MOV	B,A			; SAVE ENTRY FLAG
	JP	CDS4.5			; IS NOT EMPTY OR CLEAR
	MVI	M,DF.CLR		; IS CLEAR NOW
CDS4.5	CALL	CDS6.			; A  = DIRECTORY ENTRY LENGTH
	CALL	$DADA.
	JMP	CDS4

*	BLOCK IS CORRECTED. WRITE BACK TO DISK

CDS5	PUSH	B			; SAVE (B) FLAG
	LXI	B,512
	LHLD	S.SCR
	XCHG				; DE = SCRATCH POINTER
	LXI	H,DIS.SEC
	DAD	D			; HL = POINTER TO DIS.SEC
	CALL	$HLIHL			; HL = SECTOR
	MVI	A,DC.WRI
	CALL	DRIVER			; WRITE BLOCK BACK
	POP	B
	RC				; CAN'T WRITE IT, FORGET IT

*	IF THE LAST ENTRY IN THIS BLOCK IS NOT CLEAR, MUST CONTINUE
*	CORRECTIONS TO NEXT BLOCK

	MVI	A,DF.CLR
	CMP	B
	RE				; ALL CLEAR

	LHLD	S.SCR
	CALL	$INDL
	DW	DIS.LNK
	XCHG				; HL = LINK SECTOR; DE = SCR.

	MOV	A,H
	ORA	L
	RZ				; NO MORE TO CORRECT

	LXI	B,512
	XRA	A
	ERRNZ	DC.REA
	CALL	DRIVER			; READ NEXT BLOCK
	RC				; ERROR

	LHLD	S.SCR			; HL = SCRATCH POINTER
	JMP	CDS4			; TRY THIS ONE
	SPACE	4,10
**	CDS6.	-  GET DIRECTORY ENTRY LENGTH

CDS6.	PUSH	H
	LHLD	S.SCR
	CALL	$INDLB
	DW	DIS.ENL
	POP	H
	RET
