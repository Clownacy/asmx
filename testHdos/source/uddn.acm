UDDN	SPACE	4,10
**	$UDDN - UNPACK DECIMAL DIGITS.
*
*	UDDN CONVERTS A 16 BIT VALUE INTO A SPECIFIED NUMBER OF
*	DECIMAL DIGITS. THE RESULT IS NULL FILLED TO THE LEFT.
*
*	ENTRY	(BC) = ADDRESS VALUE
*		(A) = DIGIT COUNT
*		(HL) = MEMORY ADDRESS
*	EXIT	(HL) = (HL) + (A)
*	USES	ALL

$UDDN	EQU	*

	CALL	$DADA
	PUSH	H			; SAVE FINAL (HL) VALUE

UDDN1	PUSH	PSW
	PUSH	H
	LXI	D,10
	CALL	$DU66			; (HL) = VALUE/10
	MOV	B,H
	MOV	C,L			; (BC) = QUOTIENT
	POP	H
	MVI	A,'0'
	ADD	E			; ADD REMAINDER
	DCX	H
	MOV	M,A			; STORE DIGIT
	MOV	A,B
	ORA	C
	JZ	UDDN2			; ALL ZEROS
	POP	PSW
	DCR	A
	JNZ	UDDN1			; IF MORE TO GO

*	ALL DONE. EXIT

UDDN1.5 POP	H			; RESTORE H
	RET

*	DIGITS LEADING THIS ONE ARE ZERO. STORE NULLS INSTEAD.

UDDN2	POP	PSW
UDDN3	DCR	A
	JE	UDDN1.5 		; ALL DONE
	DCX	H
	MVI	M,0
	JMP	UDDN3
