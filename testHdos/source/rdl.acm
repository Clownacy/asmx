	SPACE	4,10
**	RDL - REQUEST DEVICE DRIVER.
*
*	RDL SETS A REQUEST FOR THE LOADING OF A DEVICE DRIVER.
*
*	THE DRIVER IS LOADED INTO MEMORY JUST BELOW *S.SYSM*.
*
*	ENTRY	(DE) = #DEV.RES
*	EXIT	'C' SET IF ERROR
*		 (A) = ERROR CODE
*		'C' CLEAR IF OK
*		 DEVLST POINTERS SET
*	USES	A,F,B,C,H,L

RDL	EQU	*

	LDA	S.MOUNT
	ANA	A			; HAVE SYSTEM DISK?
	MVI	A,EC.SDR		; ASSUME SYSTEM DISK IS RESET
	STC
	RZ				; CAN'T LOAD

	PUSH	D			; SAVE (DE)
	XCHG				; (HL) = #DEV.RES
	SHLD	S.DDDTA 		; SET DEVICE TABLE ADDRESS (OF DEV.RES)
	LXI	D,DEV.DVL-DEV.RES
	DAD	D			; (HL) = ADDRESS OF LENGTH
	MOV	E,M
	INX	H
	MOV	D,M			; (DE) = LEN OF DRIVER
	ERRNZ	DEV.DVG-DEV.DVL-2
	INX	H

	MOV	A,M			; (A) = (DEV.DVG)
	STA	S.DDGRP 		; SET GROUP FOR FILE

	CALL	AGT			; HL = load address

	MVI	A,EC.NRD		; NO ROOM FOR DRIVER
	JC	RDL1			; ERROR

*	SEE IF THIS IS ABOVE THE USER HIMEM

	XCHG				; (DE) = NEW S.SYSM
	SHLD	S.DDLEN 		; SET LENGTH OF LOAD
	LHLD	S.USRM
	INX	H

	MOV	A,E
	SUB	L
	MOV	A,D
	SBB	H
	MVI	A,EC.NRD
	JC	RDL1			; NO ROOM

	LHLD	S.SYSM
	XCHG
	SHLD	S.DDLDA 		; SET LOAD ADDRESS
	DCX	H
	MOV	M,D			; INSTALL POINTER
	DCX	H			;  TO PREVIOUS
	MOV	M,E			;   S.SYSM
	SHLD	S.SYSM			; SET NEW S.SYSM

RDL1	POP	D			; RESTORE (DE)
	RET
