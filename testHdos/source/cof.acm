	SPACE	4,10
**	COF	- CHECK OUTPUT FILE
*
*	CHECK THE SPECIFIED DEVICE FOR ANY OPEN FILES.
*
*
*	ENTRY:	DEVICE and UNIT initialized
*
*	EXIT:	(PSW)	= 'C' CLEAR IF NO OPEN FILES
*			= 'C' SET   IF    OPEN FILES
*			   A  = ERROR MESSAGE
*
*	USES:	(PSW),(DE)
*

COF	PUSH	B
	PUSH	H

*	INITIALIZE COMPARE STRING

	LHLD	DEVNAME
	SHLD	COFA			; STUFF DEVICE
	LDA	UNIT
	STA	COFB			; STUFF UNIT
	ERRNZ	IOC.UNI-IOC.DEV-2

	LHLD	S.CFWA			; FIRST WORD OF CHANNEL TABLE

COF1	PUSH	H
	LXI	D,IOC.FLG
	DAD	D
	MOV	A,M
	ANA	A
	JZ	COF2			; FILE NOT OPEN
	MVI	A,IOC.DEV-IOC.FLG
	CALL	$DADA.			; (HL) = ENTRY OF CHANNEL AND UNIT IN TABLE
	MVI	C,3
	LXI	D,COFA			; (DE) = COMPARISON STRING ADDRESS
	CALL	$COMP
	JZ	COF3			; HAVE A MATCH

COF2	POP	H			; (HL) = IOC.LNK
	ERRNZ	IOC.LNK
	CALL	$HLIHL
	MOV	A,L
	ORA	H
	JNZ	COF1
	POP	H
	POP	B
	RET

COF3	POP	H
	STC
	MVI	A,EC.FOD		; FILE OPEN ON DEVICE
	POP	H
	POP	B
	RET

COFA	DC	2,#0			; TEMPORARY COMPARISON STRING	/3.0a/
COFB	DC	1,#0							/3.0a/
