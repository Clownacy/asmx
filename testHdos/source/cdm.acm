	SPACE	4,10
**	CDM - CHECK FOR DEVICE MOUNTED.
*
*	CDM REFORMATS THE SUPPLIED DEVICE SPECIFICATION
*	INTO A 6 CHARACTER FIELD OF THE FORMAT
*
*	D E V : <00> <ENL>
*
*	IN 'DEVNAME'
*
*
*	ENTRY:	HL	=  ADDRESS FOR DEVICE SPECIFICATION
*
*	EXIT	(PSW)	= 'C' CLEAR IF NO ERROR
*			   (B) = BIT INDEX FOR THE SPECIFIED UNIT
*			   (HL) = ADDREESS OF DEV.MUM CELL
*			   'Z' SET IF NOT MOUNTED
*			   'Z' CLEAR IF MOUNTED
*			= 'C' SET IF ERROR
*			   (A) = ERROR CODE
*
*	USES	ALL
*

CDM	LXI	D,$ZEROS

CDM.	CALL	DDS.			; REQUIRE NULL TERMINATOR
	RC

*	GOT VALID DEVICE.

	CALL	LUD			; HL = DEVICE TABLE ENTRY ADDR. /3.0a/
	RC				; ERROR LOOKING FOR DEVICE

	CALL	RVD
	RC				; NOT A VALID DEVICE

	CALL	$INDL
	DW	DEV.DDA
	XCHG
	SHLD	DEVENT
	XCHG

	SHLD	DEVTAB
	LXI	D,DEV.UNT
	DAD	D
	LDA	UNIT
	CALL	S.GUP
	SHLD	UNTTAB

	LHLD	DEVTAB
	LXI	D,DEV.MUM
	DAD	D
	LDA	UNIT
	MOV	B,A
	XRA	A
	CALL	BITS			; SET THE UNIT BIT
	MOV	B,A
	ANA	M			; SET FLAGS
	RET
